////////////////////////////////////////////////////////////////////////////////
//
// GENERATE VECTORS OF THE SECTIONS/SEGMENTS WHERE SYNAPSES ARE LOCATED,
// AS WELL AS THE DISTANCE FOR EACH SECTION
// 
// IN ADDITION, CREATE numScan LOCATIONS ACROSS THE TUFT, DISTRIBUTED
// AT RANDOM, TO TRACK MEMBRANE PROPERTIES
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// GENERATE OBJECTS FOR TRACKING LOCATIONS
////////////////////////////////////////////////////////////////////////////////

// GENERATE AND DEFINE OBJECTS TO TRACK SYNAPTIC LOCATIONS
objref inputSecs[numSyn] // list of sections denoting where each input is
objref inputSegs,inputDists // list of segments denoting where each input is

inputSegs = new Vector(numSyn)
inputDists = new Vector(numSyn)

// initialize distance function
{
	access somaA
	distance()
}

{
	initchannels(0) // a dummy call to initialize the point processes for NMDARs
}


for m=1,numSyn{
	inputSecs[m-1] = new String()
	
	{
		inputSegs.x[m-1] = nmda[m-1].get_loc
		inputSecs[m-1].s = secname()
		inputDists.x[m-1] = distance(inputSegs.x[m-1])
		pop_section()
	}
}


objref inputDistsSortX,inputDistsSortY
inputDistsSortX = new Vector()
inputDistsSortY = new Vector()

{
	inputDistsSortX = inputDists.sortindex()
	inputDistsSortY = inputDists.sort()
}



// GENERATE VECTORS FOR ALL SEGMENTS ACROSS THE TUFT  
// KEEP LINE 60-92 ON ALL THE TIME
// When used for recording all segments, comment out line 113-154

totalDistNeur = 0
totalDistSegs = 0
forsec distTuft {
	totalDistNeur+=1
	for (x){
		totalDistSegs+=1
	}
}
numScan = totalDistSegs

objref randSecs[numScan] // list of sections denoting where each random location is
objref randSegs,randDists // list of segments denoting where each random location is

randSegs = new Vector(numScan)
randDists = new Vector(numScan)

{
	access somaA
	distance()
}

objref scanRand
scanRand = new Random(1e7) // theSeed is defined in init.hoc; not used
{
	scanRand.uniform(0,1)
}


strdef secToAssign			// not used
objref secToAssignRef
for m=1,numScan{
	randSecs[m-1] = new String()
}


curScan = 1
forsec distTuft{
	for (x) {
		//if(x>1e-3&&x<1-1e-3){
		randSecs[curScan-1].s = secname()
		randSegs.x[curScan-1] = x
		randDists.x[curScan-1] = distance(x)
		curScan+=1
		//}
	}
}


// GENERATE VECTORS FOR NON-SYNAPTIC SEGMENTS
// When used for recording non-synaptic segments, comment out line 96-106

/*totalNonsynSegs = 0
forsec distTuft{
	for (x) {
		synFlag = 0
		for m = 1, numSyn{
			if ( !strcmp(secname(),inputSecs[m-1].s) && (abs(x - inputSegs.x[m-1]) < 1e-5) ){	
			synFlag = 1
			}
		}
		
		if (synFlag == 0){
			totalNonsynSegs+=1
		}
	}
}

numScan = totalNonsynSegs
objref randSecs[numScan]
for m = 1, numScan{
	randSecs[m-1] = new String()
}
randSegs = new Vector(numScan)
randDists = new Vector(numScan)

curScan = 1
forsec distTuft{
	for (x) {
		synFlag = 0
		for m = 1, numSyn{
			if ( !strcmp(secname(),inputSecs[m-1].s) && (abs(x - inputSegs.x[m-1]) < 1e-5) ){	
			synFlag = 1
			}
		}
		
		if (synFlag == 0){
			randSecs[curScan-1].s = secname()	
			randSegs.x[curScan-1] = x
			randDists.x[curScan-1] = distance(x)
			curScan+=1
		}
	}
}
*/


objref randDistsSortX,randDistsSortY
randDistsSortX = new Vector() 
randDistsSortY = new Vector()

{
	randDistsSortX = randDists.sortindex()
	randDistsSortY = randDists.sort()
}



////////////////////////////////////////////////////////////////////////////////
//
// DO A COMPREHENSIVE ANALYSIS OF PROPERTIES ACROSS THE MEMBRANE OVER THE
// COURSE OF THE TBS STIMULATION.
//
// ALSO:
//
// GET INTEGRALS OF SOMATIC AND DENDRITIC RECORDINGS
//
////////////////////////////////////////////////////////////////////////////////

// LOAD NECESSARY SUBROUTINES
{
	load_file("getVoltageIntegral.hoc")
}

numSec = 0
forsec distTuft{
	numSec += 1
}

// DECLARE OBJECTS
objref voltRecords[numSyn],stimSynACSF[5][numSyn],randomSynACSF[120][numSyn]

objref dendVolt,dendStimSyn[5],dendRandomSyn[120]
dendVolt = new Vector()

objref time
time = new Vector()

strdef curSect                                  
strdef strVoltToRecord,recordVoltExec

// set voltages to record
{
	dendVolt.record(&somaA.v(0.5))     // recording site at the soma
}

{
	for m=1,numSyn{
		voltRecords[m-1] = new Vector()

		curSect = inputSecs[m-1].s
		curSeg = inputSegs.x[m-1]
		
		// set up recording devices
		sprint(strVoltToRecord,"%s%s%s%g%s","&",curSect,".v(",curSeg,")")
		sprint(recordVoltExec,"%s%d%s%s%s","voltRecords[",m-1,"].record(",strVoltToRecord,")")
		execute(recordVoltExec)
	}
	
	time.record(&t)
}



////////////////////////////////////////////////////////////////////////////////
// RUN SIMULATION IN CONTROL ACSF
////////////////////////////////////////////////////////////////////////////////

objref stimSyn
stimSyn = new Vector()
stimSyn.insrt(0, 147, 84, 69, 68, 139)

/*
stimSyn.x[0] = 147
stimSyn.x[1] = 84
stimSyn.x[2] = 69
stimSyn.x[3] = 68
stimSyn.x[4] = 139
*/

objref test
test = new File()
test.wopen("runtime.txt")
runnum = 0
test.close()

// create objects for generating random sequences
objref array,ct,permutationArray
array = new Vector()
array.insrt(0,1,2,3,4,5)
arraySize = array.size()
permutationArray = new Vector()
permutationArray.append(array)
ct = new Vector()
ct.insrt(0,0,0,0,0,0)

{
	ttxBath = 0
	initchannels(ttxBath)

	// fire single synapse at different location (fixed firing time, modified synapse location)

	for n=0,4{
		ncAMPA[stimSyn.x[n]].weight = ampaWeight
		ncNMDA[stimSyn.x[n]].weight = nmdaWeight
		run()

		// runtime monitor
		test.aopen("runtime.txt")
		test.printf("run %d\n", runnum)
		runnum+=1
		test.close()
		
		dendStimSyn[n] = new Vector()	
		dendStimSyn[n].copy(dendVolt)    // the recording at the soma
	
		for m=1,numSyn{
			stimSynACSF[n][m-1] = new Vector()
			stimSynACSF[n][m-1].copy(voltRecords[m-1])
		}

		ncAMPA[stimSyn.x[n]].weight = 0
		ncNMDA[stimSyn.x[n]].weight = 0
	}

	// fire multiple synapses in different sequences (fixed synapses locations, modified firing time sequences)

	for n=0,4{
		ncAMPA[stimSyn.x[n]].weight = ampaWeight
		ncNMDA[stimSyn.x[n]].weight = nmdaWeight
	}

	// generate all possible permutations of firing sequences of 5 synapses (heap's algorithm)
	i = 1
	while (i<arraySize) {
		if (ct.x[i]<i){
			if (i%2 == 0){
				temp = array.x[0]
				array.x[0] = array.x[i]
				array.x[i] = temp
			}else{
				temp = array.x[ct.x[i]]
				array.x[ct.x[i]] = array.x[i]
				array.x[i] = temp
			}
			permutationArray.append(array)
			ct.x[i] += 1
			i = 1
		}else{
			ct.x[i] = 0
			i += 1
		}
	}

	i = 0
	for n=0,119 {
		for m=0,4 {
			ncAMPA[stimSyn.x[permutationArray.x[i]-1]].delay = 3*m
			ncNMDA[stimSyn.x[permutationArray.x[i]-1]].delay = 3*m
			i += 1
		}
		run()

		// runtime monitor
		test.aopen("runtime.txt")
		test.printf("run %d\n", runnum)
		runnum+=1
		test.close()

		dendRandomSyn[n] = new Vector()	
		dendRandomSyn[n].copy(dendVolt)    // the recording at the soma
		for m=1,numSyn{
			randomSynACSF[n][m-1] = new Vector()
			randomSynACSF[n][m-1].copy(voltRecords[m-1])
		}
	}	
}


